apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    defaultConfig {
        applicationId "com.simplemobiletools.contacts.pro"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 76
        versionName "6.13.0"
        setProperty("archivesBaseName", "contacts")
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testInstrumentationRunnerArguments clearPackageData: 'true'
    }

    signingConfigs {
        if (keystorePropertiesFile.exists()) {
            release {
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
                storeFile file(keystoreProperties['storeFile'])
                storePassword keystoreProperties['storePassword']
            }
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            if (keystorePropertiesFile.exists()) {
                signingConfig signingConfigs.release
            }
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }

    testOptions {
        execution 'ANDROIDX_TEST_ORCHESTRATOR' // Test Orchestrators
        animationsDisabled true
    }
}

dependencies {
    implementation 'com.simplemobiletools:commons:5.30.11'
    implementation 'joda-time:joda-time:2.10.1'
    implementation 'com.googlecode.ez-vcard:ez-vcard:0.10.5'
    implementation 'com.github.tibbi:IndicatorFastScroll:08f512858a'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'

    kapt "androidx.room:room-compiler:2.2.5"
    implementation "androidx.room:room-runtime:2.2.5"
    annotationProcessor "androidx.room:room-compiler:2.2.5"

    testImplementation 'junit:junit:4.13'
    testImplementation 'org.json:json:20200518'
//    testImplementation 'org.mockito:mockito-core:3.3.3'
    testImplementation 'com.squareup.okhttp3:mockwebserver:4.7.2'
    testImplementation 'org.hamcrest:hamcrest:2.2'

    androidTestImplementation 'com.squareup.okhttp3:mockwebserver:4.7.2'
//    androidTestImplementation 'org.mockito:mockito-core:3.3.3'
//    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
//    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
//    androidTestImplementation 'androidx.test:runner:1.3.0'
//    androidTestImplementation 'androidx.test:rules:1.3.0'
//    androidTestImplementation 'androidx.test.ext:truth:1.3.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    androidTestImplementation 'androidx.test:runner:1.1.0'
    androidTestImplementation 'androidx.test:rules:1.1.0'
    androidTestImplementation 'androidx.test.ext:truth:1.0.0'
    androidTestImplementation 'androidx.test.uiautomator:uiautomator:2.2.0'

//    androidTestUtil 'androidx.test:orchestrator:1.3.1-alpha01'
    androidTestUtil 'androidx.test:orchestrator:1.3.0-rc01'
}

